1 REM :H\ THEN ? THEN ? THEN ? THEN BORDER OPEN# RETURN <> THEN PI OPEN# RETURN <>{INVERSE 229}MERGE  OR {AT 1 30}!FORMAT :READ \G OR  STEP FORMAT \C{INK 252}F{INK 252}# STEP OVER \C{INK 252}F{INK 252}{INVERSE 28}#STR$ {INK 214}STR$ MOVE LLIST <>.!CONTINUE F OR RESTORE {PAPER 0}X@ OR  THEN {(C)} ~ THEN IN {FLASH 35}{BRIGHT 16}NEXT STR$ {INK 237}LLIST ### STEP L]STR$ {INK 220}<> OR F OR H:H\ THEN ? THEN ? THEN ? THEN BORDER OPEN# 6{INK 248} THEN PI OPEN# 6A{INK 248}STR$ {INK 211}STR$ <>....................................
2 REM pUSR x<LIST LPRINT <OSAVE LPRINT {pound}|8{INK 224}{-8}PAUSE ><CEp{I}{-8}STR$ CgCOPY COPY COPY COPY LIST SAVE DRAW RETURN ?{(C)}7""!COPY COPY COPY w##ACS ACS COPY CAT {-7}{-7}{-7}{-7}CIRCLE CIRCLE DRAW {O}{O}{O}RETURN  3.'COPY 3RUN jMERGE COPY 0AT TSAVE >#{(C)}COPY {PAPER 31}5dBIN {+1}COPY {-8}{-8}USR {pound}8SAVE ~COPY {-1}{-8}USR @{I}RETURN {(C)}{-8}USR {pound}{J}COPY ~{pound}USR {-8}~COPY COPY COPY COPY {-8}USR LIST !?COPY COPY COPY COPY COPY COPY  NOT COPY COPY {I}{-4}{-8}{-8}LPRINT DRAW COPY COPY ........
3 REM {INVERSE 1}TITUL.LIST{INVERSE 0}
4 BORDER 1:PAPER 0:GO SUB cls:PRINT AT 4,2;"{BRIGHT 1}{FLASH 1}DIKTATOR{BRIGHT 0}{FLASH 0}";AT 8,2;"{PAPER 0}{INK 7}Sostavleno{PAPER 7}{INK 0}"
5 PRINT AT 10,6;"{PAPER 0}{INK 7}Donom PRISTLOM{PAPER 7}{INK 0}"
7 PRINT AT 15,2;"Kopira{(C)}t {INK 1}D{INK 2}K{INK 3}'{INK 4}T{INK 5}R{INK 1}O{INK 2}N{INK 3}I{INK 4}K{INK 5}S{INK 0}"
8 PRINT AT 17,2;INK 9;"{BRIGHT 1}Perevod %. i N. RODIONOV\X{BRIGHT 0}"
9 RETURN 
10 REM {INVERSE 1}FX{INVERSE 0}
12 FOR i=1 TO 10
14 IF INT (RND *2) THEN GO TO 20
15 POKE 23779,RND *100+150:BORDER INT (RND *6)+1:LET y=USR 23780:IF PEEK 23779<175 THEN GO TO 14
16 BORDER 0
20 POKE 23779,RND *10+20:FOR a=5 TO RND *10+10:LET y=USR 23780:NEXT a
60 NEXT i
62 RETURN 
70 POKE 23779,20:FOR a=5 TO RND *10+50:LET y=USR 23780:NEXT a
72 RETURN 
80 LET co=5010:LET cls=1300:LET s$="                                "
82 GO SUB 4:LET hst=0
84 LET b$="123456789":LET c$="987654321"
110 DIM d$(49,49):DIM g$(8,22)
112 RESTORE 6101
116 FOR a=1 TO 49:READ d$(a):NEXT a
120 GO SUB co:GO SUB 4e3
130 REM {INVERSE 1}CIKL{INVERSE 0}
140 RESTORE 6150:FOR a=1 TO 8:READ g$(a):NEXT a
200 FOR a=1 TO 49:LET d$(a,1)="N":NEXT a
202 LET bk=1e3
203 LET esc=0
204 LET mpy=60
206 LET st=4
208 LET sw=0
210 LET d=10
212 LET mth=0
215 LET coy=5e3
218 LET ad=1e3
220 LET pl=1400
222 LET pc=0
224 LET cad=1980
226 LET act=790
227 LET gr=23675
228 LET bkr=900
230 LET tran=1600
232 LET pr=1700
234 LET ass=1500
236 LET aid=2060
238 LET dec=2500
240 LET rev=1800
242 LET end=3000
244 LET str=10
246 LET tune=1570
247 LET news=2750
248 LET cls=1300
249 LET war=4200
250 BORDER 1:PAPER 7:GO SUB cls
252 PRINT AT 3,4;PAPER 5;FLASH 1;"DOBRO POJALOVAT~ NA POST"'''
254 PRINT "Okonhatel|n{{(C)} balans pravleniy"''
256 PRINT "luhqego DIKTATORA naqe{(C)} l&bimo{(C)}"''
258 PRINT "stran{ RITIMBANII b{l raven ";hst
259 PRINT ''
260 IF hst<=0 THEN PRINT "Tak kak @to Vaqa pervay pop{tka,"''"nesomnenno, V{ spravites| LUHQE."''
264 IF hst>=1 THEN PRINT "V{ doljn{ stremit|sy k bol|qemu."''
266 PRINT "Nahnite s otheta KAZNAHE_STVA"''
268 PRINT "i otheta POLICII (BESPLATNO)."
270 GO SUB co
272 GO SUB act:GO SUB co
274 GO SUB pr+20:GO TO 600
400 REM {INVERSE 1}POKI{INVERSE 0}
411 POKE gr,40:POKE gr+1,94:GO TO 420
412 POKE gr,168:POKE gr+1,93:GO TO 420
413 POKE gr,232:POKE gr+1,93
420 PRINT AT 6,2;"{A}{B}{C}{D}  {A}{B}{C}{D}  {A}{B}{C}{D}  {A}{B}{C}{D}  {A}{B}{C}{D}";AT 7,2;"{E}{F}{G}{H}  {E}{F}{G}{H}  {E}{F}{G}{H}  {E}{F}{G}{H}  {E}{F}{G}{H}"
422 RETURN 
600 REM {INVERSE 1}ZAPROS\{INVERSE 0}
601 LET c=1
603 LET low=INT (RND *3)+2
604 LET str=INT (RND *3)+10
610 LET mth=mth+1
612 BORDER 1:PAPER 6:GO SUB cls
613 PRINT AT 10,12;"{PAPER 5}MESYC {PAPER 7}";BRIGHT 1;mth
614 PAUSE 50
616 GO SUB pl
618 IF bk<0 THEN GO SUB bkr
620 IF bk>0 THEN LET bk=bk-mpy
622 PAPER 6:GO SUB cls
624 PRINT AT 0,0;PAPER 4;s$;s$;s$;s$;s$;AT 3,11;"{PAPER 7}{BRIGHT 1}AUDIENCIY{BRIGHT 0}"
630 LET r=INT (RND *24)+1
632 FOR a=1 TO 22:IF d$(r,1)="N" THEN GO TO 648
634 LET r=(r-INT (r/24)*24)+1:NEXT a
640 FOR a=1 TO 24:LET d$(a,1)="N":NEXT a:GO TO 630
648 LET d$(r,1)="*"
650 LET gs=INT ((r-1)/8)+1:BORDER gs
652 LET t$=g$(gs,6 TO CODE g$(gs,5)-20)
654 LET r$=d$(r)
655 GO SUB 410+gs
656 PRINT AT 10,2;"Zapros ot ";t$
662 PRINT AT 13,8;"Ne soizvolit li";AT 14,4;"VAQE PREVOSXODITEL~STVO";AT 16,0;BRIGHT 1;r$(18 TO )
666 GO SUB co:BORDER 0:GO SUB ad
670 REM {INVERSE 1}V\BOR{INVERSE 0}
684 GO SUB cls:PRINT AT 1,10;"{BRIGHT 1}{FLASH 1}{PAPER 7}  REQENIE  {FLASH 0}{BRIGHT 0}"
688 PRINT :PRINT TAB (21-LEN t$)/2;PAPER gs;t$;" prosit| Vas"
690 PRINT AT 5,0;BRIGHT 1;r$(18 TO )''BRIGHT 0;PAPER 1;s$
694 IF  NOT c THEN GO TO 730:LET a=0
696 GO SUB cad:PRINT :PRINT INK 4;s$
698 IF c=0 THEN GO TO 730
722 GO SUB coy
726 IF k$="d" THEN GO TO 760
728 GO TO 740
730 GO SUB cls:PRINT AT 10,1;"U Vas {BRIGHT 1}NEDOSTATOHNO FONDOV{BRIGHT 0}"
731 PRINT AT 12,1;"oplatit| @to {BRIGHT 1}REQENIE{BRIGHT 0}."
732 PRINT AT 15,4;"V{ doljn{ otvetit| {FLASH 1}NET{FLASH 0}":PAUSE 250
734 LET c=1
740 LET x=VAL g$(gs,1)
744 LET y=CODE (r$(gs+3))-77
746 LET x=x-y
748 IF x<0 THEN LET x=0
750 LET g$(gs,1)=STR$ x
751 GO SUB cls
752 GO SUB act:GO TO 766
760 GO SUB act:GO SUB tran:GO SUB act+6
766 GO SUB pl:GO SUB co
775 GO SUB ass:IF d=0 THEN GO TO end
778 GO SUB war:IF d=0 OR esc=1 THEN GO TO end
779 GO SUB pl
780 GO SUB pr:GO TO dec
790 REM {INVERSE 1}FINANS\{INVERSE 0}
792 PAPER 7:GO SUB cls:BORDER 4
794 FOR a=1 TO 24:PRINT INK 4;"$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$":NEXT a
796 POKE gr,104:POKE gr+1,94
798 PRINT AT 3,1;PAPER 4;"{A}{B}{C}{A}{B}{C}{A}{B}{C}{A}{B}{C}{A}{B}{C}{A}{B}{C}{A}{B}{C}{A}{B}{C}{A}{B}{C}{A}{B}{C}";AT 4,1;"{D}{E}{F}{D}{E}{F}{D}{E}{F}{D}{E}{F}{D}{E}{F}{D}{E}{F}{D}{E}{F}{D}{E}{F}{D}{E}{F}{D}{E}{F}";AT 5,1;"{G}{H}{I}{G}{H}{I}{G}{H}{I}{G}{H}{I}{G}{H}{I}{G}{H}{I}{G}{H}{I}{G}{H}{I}{G}{H}{I}{G}{H}{I}"
824 PRINT AT 8,7;"{BRIGHT 1}OTHET KAZNAHE_STVA{BRIGHT 0}"
825 PAPER 1:INK 9
826 BRIGHT 1:PRINT AT 12,1;" KAZNAHE_STVO";
828 IF INT bk>=0 THEN PRINT " imeet $";
830 IF bk<0 THEN PRINT " {FLASH 1}ZADOLJALO{FLASH 0} $";
831 PRINT ABS INT bk;",000 "
832 PRINT AT 14,1;" EJEMESYHN\E  RASXOD\ $";mpy;",000 "
838 IF sw>0 THEN PRINT AT 17,2;"(Shet v QVE_C.BANKE$";sw;",000)"
842 BRIGHT 0:RETURN 
900 REM {INVERSE 1}BANKROT{INVERSE 0}
910 GO SUB cls
914 PRINT AT 5,3;"KAZNAHE_STVO  {FLASH 1}OBANKROTILOS~{FLASH 0}"
918 PRINT AT 9,0;"Vaqa POPULYRNOST~ v ARMII i"''
920 PRINT "v TA_NO_ POLICII rezko UPADET"'''
922 PRINT " !FFEKTIVNOST~ POLICII i"''
924 PRINT " VAQEGO RUKOVODSTVA SNIZYTSY"
930 IF g$(1,1)>"0" THEN LET g$(1,1)=STR$ (VAL g$(1,1)-1)
932 IF g$(6,1)>"0" THEN LET g$(6,1)=STR$ (VAL g$(6,1)-1)
933 IF g$(6,2)>"0" THEN LET g$(6,2)=STR$ (VAL g$(6,2)-1)
934 IF st>0 THEN LET st=st-1
940 PAUSE 250:GO SUB pl:GO SUB pr
944 RETURN 
1000 REM {INVERSE 1}SOVET{INVERSE 0}
1002 PAPER 4:BORDER 1:GO SUB cls
1004 FOR a=1 TO 19:PRINT AT a,11;"{FLASH 1}{PAPER 7}?{FLASH 0} SOVET {FLASH 1}?{FLASH 0}{BRIGHT 0}":NEXT a
1010 GO SUB coy:PAPER 6:GO SUB cls
1014 IF k$<>"d" THEN RETURN 
1022 PRINT AT 1,0;INVERSE 1;d$(r,18 TO ):PRINT 
1026 PRINT AT 3,0;"{BRIGHT 1}Vaqa POPULYRNOST~ budet u{BRIGHT 0}...":PRINT 
1027 FOR a=1 TO 8
1028 LET x=CODE d$(r,a+3)-77:IF x=0 THEN GO TO 1034
1029 PRINT TAB 2;g$(a,6 TO );TAB 21;
1031 IF x>0 THEN PRINT "+";
1032 PRINT x;
1033 IF gs=a AND r<25 THEN PRINT INVERSE 1;INK gs;"< "
1034 NEXT a
1035 PRINT :PRINT 
1036 PRINT "{BRIGHT 1}!FFEKTIVNOST~{BRIGHT 0} ...":PRINT 
1038 FOR a=1 TO 6
1039 LET x=CODE d$(r,a+11)-77:IF x=0 THEN GO TO 1048
1041 PRINT TAB 2;g$(a,6 TO );TAB 21;
1042 IF x>0 THEN PRINT "+";
1044 PRINT x
1048 NEXT a
1050 GO SUB co:GO SUB cls:RETURN 
1300 REM {INVERSE 1}CLS{INVERSE 0}
1310 POKE 23659,2:CLS :POKE 23659,0:PRINT AT 22,0;s$;s$:BEEP .1,40:PRINT AT 0,0;:RETURN 
1400 REM {INVERSE 1}PLOT{INVERSE 0}
1410 IF mth<=2 THEN RETURN 
1420 FOR a=1 TO 3:LET g$(a,3 TO 4)="::":NEXT a
1426 IF mth<pc THEN RETURN 
1430 FOR a=1 TO 3
1432 IF VAL g$(a,1)>low THEN GO TO 1450
1436 FOR p=1 TO 6
1438 IF a=p OR VAL g$(p,1)>low THEN GO TO 1441
1440 IF VAL g$(p,2)+VAL g$(a,2)>=str THEN GO TO 1446
1441 NEXT p
1442 LET g$(a,3)="A":GO TO 1450
1446 LET g$(a,3)="R"
1448 LET g$(a,4)=STR$ p
1450 NEXT a
1452 RETURN 
1500 REM {INVERSE 1}POKUQENIY{INVERSE 0}
1510 LET r=INT (RND *3)+1
1514 IF g$(r,3)<>"A" THEN RETURN 
1516 PAPER 0:BORDER 0:GO SUB cls:PRINT AT 10,5;"{BRIGHT 1}{FLASH 1}POP\TKA POKUQENIY{BRIGHT 0}{FLASH 0}"
1518 PRINT AT 20,6;"odnogo iz ";g$(r,6 TO )
1520 FOR a=60 TO 1 STEP -.5:POKE 23886,a:LET i=USR 23884:NEXT a:POKE 23886,70
1522 GO SUB cls:PAUSE 30:GO SUB 70:PAUSE 50
1526 IF g$(1,3)="A" AND g$(2,3)="A" AND g$(3,3)="A" THEN GO TO 1546
1540 IF VAL g$(6,1)>low OR VAL g$(6,2)>low OR INT (RND *2) THEN GO TO 1554
1546 BORDER 0:PAPER 0:GO SUB cls:PRINT AT 12,10;"V{ UBIT\..."
1548 BEEP 3,-40
1550 LET d=0:RETURN 
1554 BORDER 0:PAPER 7:GO SUB cls:PRINT AT 12,5;"{BRIGHT 1}{FLASH 1} POKUQENIE NE UDALOS~ {BRIGHT 0}{FLASH 0}"
1556 POKE 23886,50:LET a=USR 23842
1560 RETURN 
1570 REM {INVERSE 1}ZVUK{INVERSE 0}
1572 FOR a=1 TO LEN m$ STEP 2
1574 IF CODE m$(a+1)=32 THEN PAUSE CODE m$(a)/4:GO TO 1578
1576 BEEP (CODE m$(a)-48)/6,CODE m$(a+1)-105
1578 NEXT a
1580 RETURN 
1600 REM {INVERSE 1}PEREDAHA{INVERSE 0}
1610 LET d$(r,1)="*"
1620 LET t$=d$(r,4 TO 11)
1621 FOR a=1 TO 8
1622 IF t$(a)="M" THEN GO TO 1632
1624 LET x=VAL g$(a,1)+(CODE t$(a)-77)
1626 IF x>9 THEN LET x=9
1628 IF x<0 THEN LET x=0
1630 LET g$(a,1)=STR$ x
1632 NEXT a
1638 LET t$=d$(r,12 TO 17)
1640 FOR a=1 TO 6
1642 IF t$(a)="M" THEN GO TO 1652
1644 LET x=VAL g$(a,2)+(CODE t$(a)-77)
1646 IF x>9 THEN LET x=9
1648 IF x<0 THEN LET x=0
1650 LET g$(a,2)=STR$ x
1652 NEXT a
1660 LET bk=bk+cst
1662 LET mpy=mpy-mcst
1664 IF mpy<0 THEN LET mpy=0
1666 RETURN 
1700 REM {INVERSE 1}OTHET POLICII{INVERSE 0}
1702 INK 9:BORDER 0:PAPER 0:GO SUB cls
1703 POKE gr,104:POKE gr+1,94
1705 PRINT AT 0,1;INK 6;INVERSE 1;"{J}{K}{L}{J}{K}{L}{J}{K}{L}{J}{K}{L}{J}{K}{L}{J}{K}{L}{J}{K}{L}{J}{K}{L}{J}{K}{L}{J}{K}{L}";AT 1,1;"{M}{N}{O}{M}{N}{O}{M}{N}{O}{M}{N}{O}{M}{N}{O}{M}{N}{O}{M}{N}{O}{M}{N}{O}{M}{N}{O}{M}{N}{O}"
1709 PRINT AT 6,5;"{BRIGHT 1}OTHET TA_NO_ POLICII {BRIGHT 0}";
1710 IF bk<=0 OR VAL g$(6,1)<=low OR VAL g$(6,2)<=low THEN GO TO 1764
1713 PRINT "{FLASH 1}?{FLASH 0}":PRINT AT 12,6;"( stoimost| $1000 )"
1714 GO SUB coy
1715 IF k$<>"d" THEN RETURN 
1718 LET bk=bk-1
1720 PAPER 0:GO SUB cls:BORDER 0
1721 PRINT "MESYC ";mth;AT 3,0;INK 1;FLASH 1;s$
1722 PRINT AT 3,8;INVERSE 1;"{BRIGHT 1} OTHET POLICII {BRIGHT 0}"
1723 PRINT AT 5,14;INVERSE 1;"{J}{K}{L}";AT 6,14;"{M}{N}{O}"
1724 PRINT AT 6,1;"POPULYRNOST~";AT 6,23;"SIL\"
1725 RESTORE 6200:FOR a=1 TO 8:READ k$
1726 PRINT AT 7+a,11;PAPER 6;FLASH 0+(g$(a,3)="R");k$
1728 PRINT AT 7+a,10;PAPER 7;BRIGHT 1;a
1729 IF a<=3 AND g$(a,3)="R" THEN PRINT AT 7+a,21;PAPER 7;VAL g$(a,4)
1732 LET x=VAL g$(a,1):PRINT AT 7+a,10-x;INK 7;PAPER 4;c$(10-x TO )
1734 IF a<=3 AND g$(a,3)="A" THEN PRINT AT a+7,21;PAPER 7;"{FLASH 1}{BRIGHT 1}A{BRIGHT 0}{FLASH 0}"
1736 NEXT a
1738 FOR a=1 TO 6:PRINT AT 7+a,22;PAPER 2;INK 7;b$( TO VAL g$(a,2)):NEXT a
1742 PRINT AT 17,1;"Vaqi SIL\ ";st
1744 PRINT AT 19,1;"SIL\ REVOL%CII ";str
1750 GO SUB co:PAPER 4:GO SUB cls:RETURN 
1764 PRINT AT 10,9;"NE PREDSTAVLEN"''
1766 IF VAL g$(6,1)<=low THEN PRINT TAB 3;"Vaqa POPULYRNOST~ u nas ";g$(6,1)''
1768 IF VAL g$(6,2)<=low THEN PRINT TAB 3;"Sila POLICII ";g$(6,2)''
1769 IF bk<=0 THEN PRINT TAB 2;"V{ ne mojete ZATREBOVAT~ OTHET"
1770 POKE 23886,30:FOR a=1 TO 80:LET i=USR 23884:NEXT a
1772 POKE 23886,50:RETURN 
1800 REM {INVERSE 1}REVOL%CIY{INVERSE 0}
1801 GO SUB news
1802 FOR a=1 TO 3:LET r=INT (RND *3)+1
1804 IF g$(r,3)="R" THEN GO TO 1810
1806 NEXT a
1807 GO TO 600
1810 POKE 23886,50:INK 0:BORDER 2:FLASH 1:PAPER 2:GO SUB cls:INK 9:PRINT AT 10,11;FLASH 0;"{PAPER 7}{BRIGHT 1}REVOL%CIY{BRIGHT 0}"
1812 FOR a=1 TO 5:BEEP .5,20:BEEP .5,10:NEXT a
1814 FLASH 0:PAPER 6:GO SUB cls
1816 PRINT AT 12,8;"{PAPER 7}{BRIGHT 1}POP\TKA K BEGSTVU ?{BRIGHT 0}":GO SUB coy
1830 GO SUB cls:IF k$<>"d" THEN GO TO 1850
1832 IF d$(36,1)="*" THEN GO TO 1842
1833 PRINT AT 10,2;"Vam neobxodimo probrat|sy herez";AT 12,9;"GOR\ v LEFTOTO"
1834 PAUSE 200:GO SUB cls
1836 IF INT (RND *((VAL g$(4,2)/3)+.4)) THEN GO TO 1839
1837 PRINT AT 12,0;"{FLASH 1}{BRIGHT 1}PARTIZAN\ NE SMOGLI Vas po{(C)}mat|{BRIGHT 0}{FLASH 0}":LET i=USR 23842:GO TO end
1839 PAPER 0:GO SUB cls
1840 LET d=0:PAUSE 50:GO SUB 70:PRINT AT 12,1;"PARTIZAN\ {FLASH 1}PRAZDNU%T{FLASH 0}":GO SUB 70:GO TO end
1842 IF INT (RND *3)=0 THEN GO TO 1845
1843 PRINT AT 12,3;"{FLASH 1}{BRIGHT 1}V{ SKR\VAETES~ NA VERTOLETE{BRIGHT 0}{FLASH 0}":LET i=USR 23842:GO TO end
1845 PRINT AT 10,3;"VERTOLET ne mojet VZLETET~"
1846 PAUSE 150
1847 GO SUB cls:GO TO 1833
1850 LET x=VAL g$(r,2)+VAL g$(VAL g$(r,4),2)
1852 PRINT AT 5,0;"Otryd{ ";g$(r,6 TO CODE g$(r,5)-20)'" ob]edenilis| s otrydami"
1856 PRINT g$(VAL g$(r,4),6 TO )''
1857 PRINT "Ix ob]edenennay SILA ";x
1858 PRINT ''"Na H~% POMOW~ V{ rashit{vaete ?"
1860 LET z=1:FOR a=1 TO 6
1862 IF VAL g$(a,1)<=low THEN GO TO 1868
1864 PRINT TAB 6;a;" .";g$(a,6 TO CODE g$(a,5)-20);" ?"
1866 LET z=0
1868 NEXT a
1869 IF z THEN GO TO 1890
1870 GO SUB co:IF k$<"1" OR k$>"6" THEN GO TO 1870
1878 IF VAL g$(VAL k$,1)<=low THEN GO TO 1884
1880 LET h=VAL k$:GO TO 1893
1884 GO SUB cls:PRINT AT 12,4;"V{, veroytno, qutite ":GO TO 1846
1890 GO SUB cls:PRINT AT 8,5;"V{ OSTALIS~ v ODINOHESTVE"
1892 GO SUB co
1893 GO SUB cls
1894 PRINT AT 8,0;"VAQI VOZMOJNOSTI ";st
1895 PRINT ''g$(h,6 TO CODE g$(h,5)-20);" vozmojnosti ";g$(h,2)
1896 PRINT ''"REVOL%CIONEROV ";x
1898 PAUSE 250
1900 PAPER 7:GO SUB cls:PRINT AT 12,3;"{BRIGHT 1}{FLASH 1}NAHINAETSY   REVOL%CIY{BRIGHT 0}{FLASH 0}"
1904 GO SUB 10
1914 IF x<=st+VAL g$(h,2)+INT (RND *3)-1 THEN GO TO 1950
1922 BORDER 0:PAPER 0:GO SUB cls:PRINT AT 10,7;"Vas SVERGLI"
1923 PRINT AT 12,8;"i ";FLASH 1;"LIKVIDIROVALI"
1924 GO SUB 70
1926 LET d=0:PAUSE 50:GO TO end
1950 BORDER 0:GO SUB cls:PRINT AT 10,3;"{BRIGHT 1}{FLASH 1}PEREVOROT POTERPEL PROVAL{FLASH 0}{BRIGHT 0}"
1952 LET a=USR 23842
1954 PRINT AT 0,0;:FOR a=1 TO 22:PRINT PAPER INT (RND *5)+1;s$:NEXT a
1955 PRINT AT 10,0;"{+8}{+8}{BRIGHT 1}KAZNIT~ REVOL%CIONEROV ?{BRIGHT 0}{+8}{+8}"
1956 GO SUB coy:IF k$<>"y" THEN GO TO 1968
1960 FOR n=1 TO 3:GO SUB 70:LET y=SQR PI /PI :NEXT n
1964 LET g$(r,1 TO 2)="00"
1966 LET g$(VAL g$(r,4),1 TO 2)="00"
1968 LET g$(h,2)="9"
1969 LET pc=mth+2:GO SUB pl:GO SUB pr:GO TO 600
1980 REM {INVERSE 1}FINANSOV\_ SOVET{INVERSE 0}
1990 LET cst=10*(CODE d$(r,2)-77)
1992 LET mcst=CODE d$(r,3)-77
1994 IF  NOT cst AND  NOT mcst THEN GO TO 2030
1996 PRINT AT 9,1;"!to reqenie ":PRINT 
1998 IF  NOT cst THEN GO TO 2014
2002 IF cst>0 THEN PRINT " PRINESET KAZNE ";
2004 IF cst<0 THEN PRINT " OTNIMET iz KAZN\ ";
2008 PRINT " $";ABS cst;",000":PRINT 
2010 IF  NOT mcst THEN GO TO 2020
2012 PRINT " i";
2014 IF mcst<0 THEN PRINT " UVELIHIT"'
2016 IF mcst>0 THEN PRINT " UMEN~QIT"'
2018 PRINT " MESYHN\E RASXOD\ na $";ABS mcst;",000"
2020 IF bk+cst>0 THEN LET c=1:RETURN 
2021 IF (cst<0 OR mcst<0) AND (bk+cst<0 OR bk+mcst<0) THEN GO TO 2034
2022 LET c=1:RETURN 
2030 LET c=1:PRINT AT 10,7;"BEZ DENEJN\X ZATRAT":RETURN 
2034 PAUSE 250:LET c=0:GO SUB cls
2037 PRINT AT 5,0;d$(r,18 TO )
2039 PRINT AT 8,2;"STOIMOST~ @togo reqeniy"
2040 PRINT :PRINT TAB 4;" neposil|na KAZNAHE_STVU"'''
2042 IF d$(38,1)="N" THEN PRINT " Mojet b{t|, pomogut RUSSKIE ?":PRINT 
2044 IF d$(39,1)="N" THEN PRINT "AMERIKANC\ - wedr{{(C)} narod ":PRINT 
2046 PAUSE 350:RETURN 
2060 REM {INVERSE 1}POMOW~{INVERSE 0}
2062 BORDER 2:PAPER 6:GO SUB cls:PRINT :PRINT PAPER 2;"  ZAPROS OB INOSTRANNO_ POMOWI  "
2064 PRINT AT 12,13;FLASH 1;"JDITE":PAUSE 50
2066 LET x=VAL k$-1
2068 IF  NOT x THEN LET m$="2g2d3i4d2 2g2d3i4d"
2070 IF x THEN LET m$="2m1j3f3j3m4r1 2v1t3r3j3l4m"
2072 GO SUB tune
2088 IF mth<INT (RND *5)+3 THEN GO TO 2138
2112 IF d$(38+x,1)="*" THEN GO TO 2128
2113 IF VAL g$(7+x,1)<=low THEN GO TO 2132
2114 IF  NOT x THEN PRINT AT 12,0;"RUSSKIE";
2115 IF x THEN PRINT AT 12,0;"AMERIKANC\";
2116 PRINT " predostavly&t Vam "
2117 LET y=VAL g$(7+x,1)*30+INT (RND *200)
2118 PRINT AT 14,7;y;",000 DOLLAROV"
2120 LET bk=bk+y
2124 LET d$(38+x,1)="*":GO TO 2140
2128 PRINT AT 12,5;"V DOLG bol|qe NE DAEM":GO TO 2140
2132 IF  NOT x THEN PRINT AT 12,13;PAPER 2;"NET"
2134 IF x THEN PRINT AT 12,12;PAPER 1;"""NOU"""
2136 GO SUB co:RETURN 
2138 PRINT AT 12,2;"Sliqkom rano htob{ POMOGAT~"
2140 GO SUB co:RETURN 
2500 REM {INVERSE 1}REQENIE{INVERSE 0}
2506 PAPER 6:GO SUB cls
2510 BORDER 1:FOR a=1 TO 22:PRINT INVERSE 1;INK 2;"********************************":NEXT a
2522 PRINT AT 3,7;"{BRIGHT 1}{PAPER 1}{FLASH 1}REQENIE PREZIDENTA{PAPER 7}{FLASH 0}{BRIGHT 0}"
2530 INK 9:PRINT AT 6,1;"Pop{tat|sy..."
2534 PRINT AT 8,4;"1. UGODIT~ odno{(C)} GRUPPE "
2536 PRINT AT 10,4;"2. UGODIT~ vsem GRUPPAM "
2538 PRINT AT 12,4;"3. ULUHQIT~ svoi QANS\  "
2540 PRINT AT 14,4;"4. UKREPIT~ FINANS\     "
2542 PRINT AT 16,4;"5. USILIT~ odnu GRUPPU  "
2550 GO SUB co:GO SUB cls
2560 IF k$<"1" OR k$>"5" THEN GO TO 2746
2562 IF k$="1" THEN LET x$="-2"
2564 IF k$="2" THEN LET x$="35"
2566 IF k$="3" THEN LET x$="69"
2568 IF k$="4" THEN LET x$=":<"
2570 IF k$="5" THEN LET x$="=?"
2572 PRINT AT (20-(((CODE x$(2)-20)-(CODE x$(1)-20))*3))*.5,0
2574 LET y=1:LET z=1
2578 FOR a=CODE x$(1)-20 TO CODE x$(2)-20
2580 IF d$(a,1)="*" THEN GO TO 2592
2582 LET z=0
2588 PRINT y;".":PRINT d$(a,18 TO ):PRINT 
2593 LET y=y+1
2594 NEXT a
2596 IF z THEN GO TO 2600
2597 GO SUB co:GO TO 2620
2600 PRINT AT 12,1;"{FLASH 1}!TOT RAZDEL POLNOST~% ISHERPAN{FLASH 0}"
2602 PAUSE 150:GO TO dec
2605  
2628 IF k$<"0" OR k$>"6" THEN GO TO dec
2630 IF VAL k$<1 OR VAL k$>(CODE x$(2)-20)+1-(CODE x$-20) THEN GO TO dec
2634 LET r=((CODE x$)-20)+VAL k$-1
2635 IF d$(r,1)="*" THEN GO TO dec
2636 IF r=38 OR r=39 THEN GO TO 2670
2642 IF r=37 THEN GO TO 2680
2644 GO SUB ad
2646 PRINT AT 4,0;BRIGHT 1;d$(r,18 TO )
2648 GO SUB cad
2650 IF  NOT c THEN PAUSE 200:GO TO dec
2651 PRINT AT 4,0;BRIGHT 1;d$(r,18 TO )
2652 GO SUB coy
2653 IF k$<>"d" THEN GO TO dec
2654 IF r=35 THEN GO TO 2674
2656 GO SUB act:GO SUB tran
2660 GO TO 2740
2670 GO SUB aid:GO SUB act:GO TO 2741
2674 LET st=st+2:GO SUB act
2676 GO SUB tran+20:GO TO 2740
2680 GO SUB cls:PRINT AT 3,0;INVERSE 1;"POPOLNIT~ SHET V QVE_C. BANKE"'''
2685 LET x=INT (bk/2):IF x<1 THEN GO TO 2720
2687 PRINT "V KAZNAHE_STVE $";INT bk;",000"
2690 LET sw=sw+x:LET bk=bk-x
2692 PAUSE 100
2694 PRINT :PRINT :PRINT "V QV. BANK PEREVEDENO $";x;",000":GO TO 2736
2720 PRINT AT 12,8;"PEREVOD NE SDELAN":PAUSE 100
2740 GO SUB 825
2741 GO SUB pl:GO SUB co
2746 GO SUB pr:GO TO rev
2750 REM {INVERSE 1}NOVOSTI{INVERSE 0}
2760 IF INT (RND *3) THEN RETURN 
2764 LET r=INT (RND *6)+44
2768 FOR a=1 TO 6
2770 IF d$(r,1)="N" THEN GO TO 2780
2772 LET r=r+1:IF r=50 THEN LET r=44
2774 NEXT a
2776 RETURN 
2780 BORDER 1:PAPER 7:INK 0:FLASH 1:GO SUB cls:FLASH 0:PRINT AT 10,7;"{BRIGHT 1}POSLEDNIE IZVESTIY{BRIGHT 0}"
2782 FOR a=1 TO 10:BEEP .6,30:NEXT a
2784 GO SUB cls:PRINT AT 10,7;"POSLEDNIE {FLASH 1}IZVESTIY{FLASH 0}"
2786 PRINT AT 14,0;d$(r,18 TO )
2788 INK 9:PAUSE 100
2796 GO SUB tran:GO SUB pl
2800 GO SUB pr
2806 RETURN 
3000 REM {INVERSE 1}KONEC{INVERSE 0}
3004 IF d THEN PAUSE 100:GO TO 3026
3010 PAUSE 50:LET m$="4d3d1d3d3g1f2f1d2d1d5d"
3012 GO SUB tune
3026 LET x=0:FOR a=1 TO 8:LET x=x+VAL g$(a,1):NEXT a
3030 BORDER 5:PAPER 6:GO SUB cls
3035 PRINT AT 3,2;"{PAPER 7}{BRIGHT 1}{FLASH 1}REZUL~TAT\ Vaqego PRAVLENIY{BRIGHT 0}{FLASH 0}"
3050 PRINT :PRINT " Itogovay POPULYRNOST~ - ";TAB 28;x
3052 PRINT :PRINT " MESYC\ PRAVLENIY (";mth;"x3) - ";TAB 28;mth*3:LET x=x+mth*3
3058 IF  NOT d THEN GO TO 3072
3062 PRINT :PRINT " V JIV\X - ";TAB 28;d
3068 PRINT :PRINT " Za {PAPER 4}{INK 7}GRABEJ NARODA{INK 0}{PAPER 6} "'"     ($";sw;",000 /10,000) -";TAB 28;INT (sw/10){PAPER 7}
3070 LET x=x+d+INT (sw/10)
3074 PRINT :PRINT :PRINT " Obwi{(C)} SHET ";TAB 28;BRIGHT 1;x
3082 IF x>hst THEN LET hst=x
3084 PRINT :PRINT "( V{sq{{(C)} rezul|tat ";hst;" )"
3086 GO SUB co:GO SUB cls
3090 BORDER 0:PAPER 0:GO SUB cls:PRINT AT 1,14;FLASH 1;"FINAL"
3092 GO SUB pr+22:GO SUB 4:GO SUB co:GO SUB cls:GO TO 140
4000 REM {INVERSE 1}TITUL~N\_ LIST{INVERSE 0}
4002 GO SUB cls
4010 PRINT AT 0,0;:FLASH 1
4020 BORDER 0:INK 9:PAPER 5:FOR a=1 TO 48
4030 PRINT "{INVERSE 1}DIKTATOR{INVERSE 0}DIKTATOR";
4045 BEEP .01,40-a+RND *10
4050 NEXT a
4060 POKE 23659,2:FOR a=1 TO 50:BEEP .03,a:NEXT a
4070 FLASH 0:FOR a=1 TO 12
4080 PRINT AT a,7;PAPER 4;INK 2;"{+3}{+3}{+3}{+3}{+3}{+3}{+3}{+3}{+3}{+3}{+3}{+3}{+3}{+3}{+3}{+3}{+3}{+3}"
4090 NEXT a
4100 FOR a=1 TO 12:PRINT AT a,11;PAPER 1;"          ":NEXT a
4110 PRINT AT 15,5;PAPER 7;"Htob{ stat| DIKTATOROM";AT 17,6;"najmi l&bu& klaviqu"
4120 PAPER 4:PRINT AT 3,11;"RESPUBLIKA";AT 9,11;"RITIMBANIY"
4130 PRINT AT 5,13;PAPER 1;INK 6;"******";AT 6,13;"  **  ";AT 7,13;"******"
4140 LET t$="KPKKMKIHK{pound}KMRPOMOP"
4150 FOR a=1 TO LEN t$
4155 IF INKEY$ <>"" THEN RETURN 
4160 LET x=CODE t$(a)-80
4170 IF x=16 THEN PAUSE 20:GO TO 4190
4180 BEEP .5,x
4190 NEXT a
4192 PAUSE 30:GO TO 4150
4200 REM {INVERSE 1}VO_NA{INVERSE 0}
4208 IF VAL g$(5,1)>low THEN RETURN 
4210 IF VAL g$(5,2)<low THEN RETURN 
4212 IF  NOT INT (RND *3) THEN GO TO 4250
4220 BORDER 5:PAPER 0:INK 7:FLASH 1:GO SUB cls:FLASH 0:INK 9
4224 PRINT AT 6,3;"{PAPER 7}UGROZA VO_N\ s LEFTOTO"
4226 PRINT AT 10,0;"Vaqa POPULYRNOST~ v RITIMBANII";AT 12,11;"{BRIGHT 1}VOZRASTET{BRIGHT 0}"
4230 POKE 23886,255:FOR a=1 TO 5:LET i=USR 23884:NEXT a
4234 FOR a=1 TO 3:GO SUB 4242:NEXT a
4236 LET a=6:GO SUB 4242
4240 RETURN 
4242 IF g$(a,1)<"9" THEN LET g$(a,1)=STR$ (VAL g$(a,1)+1)
4244 RETURN 
4250 REM {INVERSE 1}SOB\TIY{INVERSE 0}
4270 PAPER 2:BORDER 0:GO SUB cls:PRINT AT 8,7;"{FLASH 1}{BRIGHT 1}{PAPER 7} VTORJENIE LEFTOTO {FLASH 0}{BRIGHT 0}"
4276 LET rs=0:FOR a=1 TO 3:IF VAL g$(a,1)>low THEN LET rs=rs+VAL g$(a,2)
4278 NEXT a
4280 IF VAL g$(6,1)>low THEN LET rs=rs+VAL g$(6,2)
4282 LET rs=rs+st
4286 PRINT AT 12,5;"Mow| Ritimbanii ";rs
4290 LET ls=0:FOR a=1 TO 6:IF VAL g$(a,1)<=low THEN LET ls=ls+VAL g$(a,2)
4292 NEXT a
4294 PRINT AT 14,5;"Mow| Leftoto  ";ls
4296 PRINT AT 18,3;"{BRIGHT 1}{PAPER 7}KOROTKAY REQITEL~NAY VO_NA{BRIGHT 0}"
4298 GO SUB 10
4300 IF ls+INT (RND *3-1)>=rs THEN GO TO 4320
4310 POKE 23886,50:BORDER 0:GO SUB cls:PRINT AT 12,7;"{PAPER 7}{BRIGHT 1}{FLASH 1} RAZGROM LEFTOTO {BRIGHT 0}{FLASH 0}":LET i=USR 23842
4312 LET g$(5,2)="0":RETURN 
4320 BORDER 0:PAPER 0:GO SUB cls
4321 PRINT AT 7,9;"{FLASH 1}{BRIGHT 1} POBEDA LEFTOTO {FLASH 0}{BRIGHT 0}"
4323 POKE 23886,255:FOR a=1 TO 5:LET i=USR 23884:NEXT a
4324 IF d$(36,1)="*" AND INT (RND *3) THEN GO TO 4340
4326 LET d=0:IF d$(36,1)="*" THEN PRINT AT 10,3;"OTKAZ DVIGATELE_ vertoleta":PAUSE 80
4327 PRINT AT 12,0;"VAS ob]yvili VRAGOM NARODA i..."''
4328 PAUSE 30:GO SUB 70:PRINT AT 18,7;"Nemedlenno KAZNILI"
4330 RETURN 
4340 GO SUB cls:PRINT AT 12,3;"{FLASH 1}{BRIGHT 1}V{ SKR\VAETES~ na VERTOLETE{BRIGHT 0}{FLASH 0}":POKE 23886,70:LET i=USR 23842:LET esc=1:RETURN 
5000 REM {INVERSE 1}UPRAVLENIE{INVERSE 0}
5006 PRINT AT 20,11;PAPER 7;"{BRIGHT 1}{FLASH 1} ""D""= DA {BRIGHT 0}{FLASH 0}"
5010 IF INKEY$ <>"" THEN GO TO 5010
5012 PRINT AT 21,0;PAPER 2;s$;"            KLAVIQA             ";s$
5014 FOR a=1 TO 15
5015 IF INKEY$ <>"" THEN GO TO 5030
5016 NEXT a
5017 BEEP .01,30
5018 PRINT AT 21,0;PAPER 1;s$;"            KLAVIQA             ";s$
5019 FOR a=1 TO 15
5020 IF INKEY$ <>"" THEN GO TO 5030
5024 NEXT a
5025 BEEP .01,20
5026 GO TO 5012
5030 LET k$=INKEY$ :IF k$="" THEN GO TO 5018
5034 IF k$="d" THEN BEEP .25,30
5036 IF k$<>"d" THEN BEEP .25,10
5037 IF k$="#" THEN POKE 23659,2:PAPER 7:BORDER 7:STOP 
5038 IF k$="STOP " THEN GO TO 80
5100 RETURN 
6000 REM {INVERSE 1}DANN\E{INVERSE 0}
6101 DATA "NMHQJLMMMMMPKLMMMPROVESTI MOBILIZACI%"
6102 DATA "NMMPMJMMMMMNMLMMMREKVIZIROVAT~ UGOD~Y POD MANEVR\"
6103 DATA "NCMPLNMLMLMNMNIMMATAKOVAT~ VSE BAZ\ PARTIZAN"
6104 DATA "NEMPLMMIMLMNMNKMMRAZBIT~ BAZ\ PARTIZAN V LEFTOTO"
6105 DATA "NMMQONMMIMMNMNMMJUVOLIT~ QEFA TA_NO_ POLICII"
6106 DATA "NMMPMMMLMIOMMMMMMV\SLAT~ RUSSKIX VOEN. SOVETNIKOV"
6107 DATA "NMDQMLMMMMMOLLLMMPOV\SIT~ DOVOL~STVIE VOENN\M"
6108 DATA "NAMQLLMLLMMPLLKLMOBNOVIT~ ORUJIE I BOEPRIPAS\"
6109 DATA "NMMLONMMMMMLMMLMMUREZAT~ VLAST~ VOENN\X"
6110 DATA "NMMMQIMNMMMMOLMMMPOV\SIT~ MIN.PROJITOHN\_ UROVEN~"
6111 DATA "NMPNQOMMIMMNNNNMJOGRANIHIT~ SIL\ TA_NO_ POLICII"
6112 DATA "NMMMPKMKMMMMOKMMMZAPRETIT~ IMMIGRACI% IZ LEFTOTO"
6113 DATA "NCELQKMOLNMMNLLMMVVESTI BESPLATNOE OBRAZOVANIE"
6114 DATA "NMMMQJMNLNMMPJMMLUZAKONIT~ SOZDANIE PROFSO%ZOV"
6115 DATA "NMMLQKMNLMMMOLLMMOSVOBODIT~ IX LIDERA IZ T%R~M\"
6116 DATA "NMSMPLMMMMMMMMLMMRAZ\GRAT~ OBWESTVENNU% LOTERE%"
6117 DATA "NMMKMPMMMMMLMMMMMOSTANOVIT~ MILITARIZACI% UGODI_"
6118 DATA "NMMMIQMLMLMMKONMMUREZAT~ MIN.PROJITOHN\_ UROVEN~"
6119 DATA "NWHMMPMNMOIMMNMMMNACIONALIZIROVAT~ PREDPRIYT. SQA"
6120 DATA "NMRMMPMJMLMMNOMLMOBLOJIT~ POQLINO_ IMPORT LEFTOTO"
6121 DATA "NMQNNPMMIMMNMNNMKSOKRATIT~ B%DJET TA_NO_ POLICII"
6122 DATA "NMHMMQMMMMMMMOMMMSOKRATIT~ ZEMEL~N\_ NALOG"
6123 DATA "NMMKLPMMMMMLLNNMMPRIVLEH~ ARMI% K S/X RABOTAM"
6124 DATA "NACNNPMJMONMMPMKMPOSTROIT~ IRRIGACIONNU% SISTEMU"
6125 DATA "NMMQLLMMLMMNMMLMLSDELAT~ VICEPREZIDENTOM VOENNOGO"
6126 DATA "NLILQNMOMNMMMMLMMOTKR\T~ BESPLATN\E BOL~NIC\"
6127 DATA "NMMLKQMMLLMLLOMMLDAT~ POMEWIKAM SAMOUPRAVLENIE"
6128 DATA "NRMKMMMQMKNLMMLPMPRODAT~ LEFTOTO AMERIK. ORUJIE "
6129 DATA "NYMMMLMLMKPMMMMMMPRODAT~ QAXT\ FIRMAM SQA"
6130 DATA "NMWKMMMMMPJMMMMNMDAT~ RUSSKIM VOENNO-MORSKU% BAZU"
6131 DATA "NMENPPMMMMMLMMLMMSNIZIT~ OBWI_ UROVEN~ NALOGOV"
6132 DATA "NEMPPPMMMMMMMMLMMPROVESTI KAMPANI% POPULYRIZACII"
6133 DATA "NMUPPPMMDMMONNNMDLIKVIDIROVAT~ TA_NU% POLICI%"
6134 DATA "NMGJJJMMUMMLLLLMUREZKO USILIT~ TA_NU% POLICI%"
6135 DATA "NIMKLLMMLMMKMMMMLUSILIT~ LIHNU% OXRANU          *"
6136 DATA "NAMIIJMMKMMMMMMMMPRIOBRESTI VERTOLET DLY POBEGA"
6137 DATA "NMMMMMMMMMMMMMMMMPOPOLNIT~ SHET V QVE_C. BANKE  *"
6138 DATA "NMMMMMMMMMMMMMMMMPROSIT~ ZA_MA U RUSSKIX"
6139 DATA "NMMMMMMMMMMMMMMMMPROSIT~ POMOWI U AMERIKANCEV"
6140 DATA "NZMNNPMGMKMMMMMMMNACIONALIZIROVAT~ PREDPR.LEFTOTO"
6141 DATA "NHMPMMMJMLMRMMKKLPRIOBRESTI TYJELU% ARTILERI%"
6142 DATA "NMMMPLMMLMMMRLPMLRASQIRIT~ PRAVA KREST~YN"
6143 DATA "NMMLLPMMLMMLLRLMLPOZVOLIT~ POMEWIKAM SVO% MILICI%"
6144 DATA "NMMMMMMMIMMMMMQMIPREZIDENT TERYET DOS~E T.POLICII"
6145 DATA "NMMMMMMMMMMLMMVMM   KUBINC\ VOORUJA%T PARTIZAN"
6146 DATA "NMMMMMMMMMMIMMOMN    VZR\V ARME_SKIX BARAKOV"
6147 DATA "NMMMMMMMMMMMMJMKM  REZKO PADA%T CEN\ NA BANAN\"
6148 DATA "NMMMMMMMMMMMMOMIMSIL~NOE ZEMLETRYSENIE V LEFTOTO"
6149 DATA "NMMMMMMMMMMMILKMMKREST~YNE STRADA%T OT NAVODNENIY"
6150 DATA "76::!ARMII"
6151 DATA "76::%KREST~YN"
6152 DATA "76::'POMEWIKOV"
6153 DATA "06--&PARTIZAN"
6154 DATA "76--&LEFTOTANCEV"
6155 DATA "76--*TA_NO_ POLICII"
6156 DATA "7---%RUSSKIX"
6157 DATA "7---&AMERIKANCEV"
6200 DATA "  ARMIY   ","KREST~YNE "," POMEWIKI ","PARTIZAN\ "
6210 DATA "LEFTOTANC\","T.POLICIY "," RUSSKIE  ","AMERIKANC\"
7000 REM {INVERSE 1}ZAPIS~{INVERSE 0}
7010 SAVE "dictator"CODE 22528,25200
7012 IF INKEY$ ="q" THEN GO TO 7050
7046 GO TO 80
7050 IF INKEY$ <>"" THEN GO TO 7050
7060 CLEAR :POKE 23886,70:RANDOMIZE USR 23842:GO TO 7010
